plugins {
    id 'java'
    id 'com.google.protobuf' version '0.8.18'
    id 'application' // For building and running applications
}

group 'com.clustercrew'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // Core gRPC dependencies
    implementation 'io.grpc:grpc-netty:1.68.1'
    implementation 'io.grpc:grpc-protobuf:1.68.1'
    implementation 'io.grpc:grpc-stub:1.68.1'

    // Protocol Buffers dependencies
    implementation 'com.google.protobuf:protobuf-java:3.21.12'

    // Apache ZooKeeper for metadata and state storage
    implementation 'org.apache.zookeeper:zookeeper:3.8.0'

    // Apache BookKeeper for segmented write-ahead logging
    implementation('org.apache.bookkeeper:bookkeeper-server:4.17.1') {
        // exclude group: 'org.apache.bookkeeper', module: 'bookkeeper-common'
        // exclude group: 'org.apache.bookkeeper', module: 'bookkeeper-stats'
    }

    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    // JUnit for testing
    testImplementation 'junit:junit:4.13.2'
}

protobuf {
    protoc {
        // Specify the protoc binary that comes with grpc-java
        artifact = "com.google.protobuf:protoc:3.21.12"
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.68.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

sourceSets {
    main {
        java {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
    }
    test {
        java {
            srcDirs += 'build/generated/source/proto/main/java'
            srcDirs += 'build/generated/source/proto/main/grpc'
        }
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.test {
    useJUnit()
}

application {
    // Main class to run
    mainClass = 'com.clustercrew.messagequeue.MessageQueueCluster'
}
